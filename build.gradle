plugins {
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'java'
    id 'jacoco'
    id 'com.github.spotbugs' version '6.0.6'
    id "org.sonarqube" version "4.4.1.3373"
}

group = 'uk.nhs.prm.repo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '21'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

//Without this task two jars are built, the additional "-plain.jar" is not needed
// for more details refer to: https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/#packaging-executable.and-plain-archives
jar {
    enabled = false
}

dependencies {
    implementation('com.google.guava:guava') { version { strictly '32.1.3-jre' } }

    implementation('org.yaml:snakeyaml') { version { strictly '2.2' } }

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation('io.netty:netty-buffer') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-codec') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-codec-http') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-codec-http2') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-common') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-handler') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-resolver') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-transport') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-transport-classes-epoll') { version { strictly '4.1.104.Final' } }
    implementation('io.netty:netty-transport-native-unix-common') { version { strictly '4.1.104.Final' } }

    implementation 'com.amazonaws:amazon-sqs-java-messaging-lib:1.1.2'

    implementation platform('software.amazon.awssdk:bom:2.18.41')
    implementation 'software.amazon.awssdk:cloudwatch'
    implementation 'software.amazon.awssdk:sqs'
    implementation 'software.amazon.awssdk:sns'
    implementation 'software.amazon.awssdk:dynamodb'

    implementation group: 'javax.inject', name: 'javax.inject', version: '1'
    implementation 'org.springframework:spring-jms:5.3.19'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'io.github.resilience4j:resilience4j-retry:1.7.1'

    implementation 'net.logstash.logback:logstash-logback-encoder:7.2'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'com.github.spotbugs:spotbugs-annotations:4.8.3'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-test'

    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'org.wiremock:wiremock-standalone:3.3.1'
    testImplementation 'org.awaitility:awaitility:4.2.0'
}

test {
    useJUnitPlatform()
}

test.outputs.upToDateWhen {false}

sourceSets {
    integration {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        compileClasspath += sourceSets.test.output
        runtimeClasspath += sourceSets.test.output
    }
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

tasks.withType(Test) {
    useJUnitPlatform()
}

task integration(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    mustRunAfter tasks.test
}

integration.outputs.upToDateWhen {false}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    shouldRunAfter integration
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            limit {
                minimum = 0.0
            }
        }
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/model/**')
        }))
    }
}

spotbugs {
    toolVersion = '4.8.3'
}

spotbugsMain {
    ignoreFailures = true
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsTest {
    ignoreFailures = true
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

spotbugsIntegration {
    ignoreFailures = true
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/integration/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "prm-orphaned-record-continuity_prm-repo-re-registration-service"
        property "sonar.organization", "prm-orphaned-record-continuity"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

check.dependsOn integration
